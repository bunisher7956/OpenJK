;======================================================
;            Inno Setup Installer Script
;                   jku_installer
;
;------------------------------------------------------
; Author:   Fnuki
; Date:     2020-04-18 21:25
; Version:  0.1
;------------------------------------------------------
;
; First attempt to create an installer for this project
; to no longer manually copy and paste files
;======================================================

; Include the environment setup
#include "env_setup.iss"

[Setup]
; AppId generated by VisualStudio: Tools > Create GUID
AppId={{E34A262D-6E04-405E-83B8-8C0EE808BA5C}}
AppName=Jedi Knight: Unlimited
AppVerName=v0.1 Pre-Alpha
AppVersion=0.1.0.0
AppPublisher=JKU Development Team
AppPublisherURL=TBD
AppSupportURL=TBD
AppendDefaultDirName=no
DefaultDirName=C:\Program Files (x86)\jedi_knight_academy
LicenseFile=jku_license.rtf
OutputDir={#JKUBuildDirectory}\installer
OutputBaseFilename=jedi_knight_unlimited_{#JKUBuildConfigDisplayName}_setup
Compression=lzma
SolidCompression=yes
ShowLanguageDialog=auto
SetupIconFile={#JKUBuildDirectory}\installer\small.ico
WizardImageFile={#JKUBuildDirectory}\installer\large.bmp
WizardSmallImageFile={#JKUBuildDirectory}\installer\smallpic.bmp

;[Languages]
;Name: english; MessagesFile: compiler:Defaults.isl

[Code]
function MsvcRuntimeNeedsInstall(const SubKeyName : String): Boolean;
var
  IsInstalled: Cardinal;
begin
  if RegQueryDWordValue(HKEY_LOCAL_MACHINE, 'SOFTWARE\WOW6432Node\Microsoft\VisualStudio\14.0\VC\Runtimes\' + SubKeyName,
     'Installed', IsInstalled) then
    if IsInstalled = 1 then
      Result := False
    else
      Result := True
  else
    Result := True
end;

[Types]
Name: custom; Description: Custom installation; Flags: iscustom

[Components]
; Fnuki: Mandatory components
Name: uiassets; Description: JKU custom assets; Types: custom; Flags: fixed
Name: gamefiles; Description: JKU custom gamefiles; Types: custom; Flags: fixed
; Fnuki: Optional components
;        Add more sections here for 3rdparty mods to be included in this installer if needed
;        - Added rd_rend2 as an example
Name: rdrend2; Description: rd_rend2; Types: custom

[Files]

; MSVC 2015 runtime
Source: {#JKUBuildDirectory}\install_includes\vc_redist.x86.exe; DestDir: {tmp}; Flags: deleteafterinstall
Source: {#JKUBuildDirectory}\install_includes\vc_redist.x64.exe; DestDir: {tmp}; Flags: deleteafterinstall

; Component: uiassets
; Fnuki: Expecting game assets to be located in the include directory, as these cannot be commit'ed to Git for legal reasons
Source: {#JKUBuildDirectory}\install_includes\jku_assets.pk3; DestDir: {app}\jku; Flags: ignoreversion; Components: uiassets

; Component: gamefiles
Source: {#JKUBuildDirectory}\installer\readme.txt; DestDir: {app}\jku; Flags: ignoreversion isreadme; Components: gamefiles
; Fnuki: Renameing the base exe to our own executable to not overwrite the base game on the user's machine
Source: {#JKUBuildDirectory}\build\{#JKUBuildConfiguration}\openjk.x86.exe; DestDir: {app}; DestName: jku.exe; Flags: ignoreversion; Components: gamefiles
Source: {#JKUBuildDirectory}\install_includes\run_jku.bat; DestDir: {app}; Flags: confirmoverwrite; Components: gamefiles
Source: {#JKUBuildDirectory}\build\{#JKUBuildConfiguration}\cgamex86.dll; DestDir: {app}\jku; Flags: ignoreversion; Components: gamefiles
Source: {#JKUBuildDirectory}\build\{#JKUBuildConfiguration}\jampgamex86.dll; DestDir: {app}\jku; Flags: ignoreversion; Components: gamefiles
Source: {#JKUBuildDirectory}\build\{#JKUBuildConfiguration}\uix86.dll; DestDir: {app}\jku; Flags: ignoreversion; Components: gamefiles

; Component: rdrend2
;Source: <path_to_rdrend2>;      DestDir: {app}\TBD; Flags: confirmoverwrite; Components: rdrend2

[Run]
Filename: "{tmp}\vc_redist.x86.exe"; StatusMsg: Installing Visual Studio x86 Runtime Libraries...; Check: MsvcRuntimeNeedsInstall('x86')
Filename: "{tmp}\vc_redist.x64.exe"; StatusMsg: Installing Visual Studio x64 Runtime Libraries...; Check: IsWin64 and MsvcRuntimeNeedsInstall('x64')